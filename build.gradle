plugins {
  id 'application'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.springframework.boot' version '2.7.12'
  id 'uk.gov.hmcts.java' version '0.12.43'
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'org.owasp.dependencycheck' version '9.0.6'
  id 'org.sonarqube' version '4.0.0.2929'
  id 'au.com.dius.pact' version '4.3.14'
  id 'io.freefair.lombok' version '8.0.1'
  id 'nebula.lint' version '18.0.3'
  id "info.solidsoft.pitest" version '1.9.11'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
  contractTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      java.srcDir 'src/contractTest/java'
      resources.srcDir 'src/contractTest/resources'
    }
    resources.srcDir file('src/contractTest/resources')
  }
}

tasks.withType(Copy).all { duplicatesStrategy 'warn' }

tasks.withType(JavaCompile) {
  options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked"
}

test {
  useJUnitPlatform()
}

task functional(type: Test) {
  //useJUnitPlatform()
  // The above has been left as commented to highlight a problem. The build was failing on the functional tests stating
  //  that there was no functional test. The fix was found in the following:
  // https://stackoverflow.com/questions/57795263/test-events-were-not-received-when-run-tests-using-intellij
  maxParallelForks = 1
  group = 'verification'
  description = 'Executes non-destructive functional tests in AAT against a running CCD and evidence share service'
  setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
  setClasspath(sourceSets.functionalTest.runtimeClasspath)
  include "uk/gov/hmcts/reform/sscs/functional/**"
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
  useJUnitPlatform()
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath

}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

task fortifyScan(type: JavaExec)  {
  main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
  classpath += sourceSets.test.runtimeClasspath
  jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

configurations {
  functionalTestImplementation.extendsFrom(testImplementation)
  functionalTestRuntimeOnly.extendsFrom(testRuntimeOnly)
  integrationTestImplementation.extendsFrom(testImplementation)
  integrationTestRuntimeOnly.extendsFrom(testRuntimeOnly)
  smokeTestImplementation.extendsFrom(testImplementation)
  smokeTestRuntimeOnly.extendsFrom(testRuntimeOnly)
  testImplementation.exclude module: "android-json"
  testRuntimeOnly.exclude module: "android-json"
  integrationTestImplementation.exclude module: "android-json"
  integrationTestRuntimeOnly.exclude module: "android-json"

  contractTestImplementation.extendsFrom testImplementation
  contractTestRuntimeOnly.extendsFrom testRuntimeOnly
}

pmd {
  toolVersion = "6.55.0"
  ignoreFailures = true
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

pitest {
  targetClasses = ['uk.gov.hmcts.reform.sscs.service.*']
  excludedClasses = ['uk.gov.hmcts.reform.sscs.config.*',
                     'uk.gov.hmcts.reform.sscs.domain.*',
                     'uk.gov.hmcts.reform.sscs.exception.*',
                     'uk.gov.hmcts.reform.sscs.factory.*',
                     'uk.gov.hmcts.reform.sscs.health.*',
                     'uk.gov.hmcts.reform.sscs.model.*',
                     'uk.gov.hmcts.reform.sscs.SscsEvidenceShareApplication']
  excludedMethods = ['equals', 'hashcode']
  jvmArgs = ['-Xms1G','-Xmx3G']
  enableDefaultIncrementalAnalysis = true
  historyInputLocation = 'build/reports/pitest/fastermutationtesting'
  historyOutputLocation = 'build/reports/pitest/fastermutationtestingoutput'
  threads = 20
  verbose = true
  outputFormats = ['XML', 'HTML']
  timestampedReports = false
  mutationThreshold = 75
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
check.dependsOn integration
checkstyleMain.shouldRunAfter(compileJava)
test.shouldRunAfter(checkstyleTest)
integration.shouldRunAfter(checkstyleIntegrationTest)
integration.shouldRunAfter(test)
functional.shouldRunAfter(checkstyleIntegrationTest)
functional.shouldRunAfter(test)

sonarqube {
  properties {
    property "sonar.exclusions",
      "src/main/java/uk/gov/hmcts/reform/sscs/config/**," +
        "src/main/java/uk/gov/hmcts/reform/sscs/servicebus/messaging/**," +
        "src/main/java/uk/gov/hmcts/reform/sscs/domain/**," +
        "src/main/java/uk/gov/hmcts/reform/sscs/model/**," +
        "src/main/java/uk/gov/hmcts/reform/sscs/docmosis/domain/**," +
        "src/main/java/uk/gov/hmcts/reform/sscs/docmosis/config/**," +
        "src/main/java/uk/gov/hmcts/reform/sscs/SscsEvidenceShareApplication.java,"
    property "sonar.projectName", "Reform :: sscs-evidence-share"
    property "sonar.projectKey", "uk.gov.hmcts.reform:sscs-evidence-share"
    property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.pitest.mode", "reuseReport"
    property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    property "sonar.host.url", "https://sonar.reform.hmcts.net/"
  }
}

dependencyUpdates.resolutionStrategy {
  componentSelection { rules ->
    rules.all {
      def isNonStable = { String version ->
        def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
      }
      if (isNonStable(candidate.version) && !isNonStable(currentVersion)) {
        selection.reject('Release candidate')
      }
    }
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // If we set a dedicated environment variable to 'false' to be lax in our build standards, then we won't fail the build,
  //  otherwise we are strict in our build and fail it.
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'false' ? 11 : 7.0
  nvd.validForHours = 24 // on Jenkins we have 24, so if you get more vulnerabilities locally, try to normalize this value with Jenkins
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    retirejs {
      enabled = false
    }
    ossIndex {
      enabled = false
    }
  }
}

repositories {

  mavenLocal()
  mavenCentral()

  // jitpack should be last resort
  // see: https://github.com/jitpack/jitpack.io/issues/1939
  maven { url 'https://jitpack.io' }
  maven {
    url 'https://repo.spring.io/libs-milestone'
  }
}

project.ext {
  pacticipant = 'sscs_evidenceShare'
  pacticipantVersion = getCheckedOutGitCommitHash()
}

static def getCheckedOutGitCommitHash() {
  'git rev-parse --verify --shirt HEAD'.execute().text.trim()
}

task runContractTests(type: Test, description: 'Runs contract tests.', group: 'Verification') {
  onlyIf {
    return System.env.SKIP_CONTRACT_TESTS == null || System.env.SKIP_CONTRACT_TESTS == 'false'
  }
  useJUnitPlatform()
  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
  systemProperty 'pact.rootDir', "pacts"
  testLogging.showStandardStreams = true
}

task runAndPublishConsumerPactTests(type: Test, description: 'Publish pact test to broker.', group: 'Verification') {
  logger.lifecycle("Runs pact consumer Tests")

  testClassesDirs = sourceSets.contractTest.output.classesDirs
  classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.finalizedBy pactPublish

pact {
  broker {
    pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
  }
  publish {
    pactDirectory = 'pacts'
    tags = [System.getenv("PACT_BRANCH_NAME") ?: 'Dev']
    version = project.pacticipantVersion
  }
}

task installLocalGitHook(type: Copy) {
  from new File(rootProject.rootDir, 'config/git/pre-commit')
  into { new File(rootProject.rootDir, '.git/hooks')}
  fileMode 0775
}

compileJava.dependsOn installLocalGitHook

task runGitPreCommitTasks {
  dependsOn 'test'
  dependsOn 'pmdMain'
  dependsOn 'pmdTest'
  dependsOn 'dependencyCheckAggregate'
  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
}

dependencies {

  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-validation'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail'

  // https://mvnrepository.com/artifact/org.springframework.retry/spring-retry
  implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.4'

  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.14'
  implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
  implementation group: 'io.rest-assured', name: 'rest-assured', version: '5.3.0'
  implementation group: 'io.rest-assured', name: 'json-path', version: '5.3.0'
  implementation group: 'io.rest-assured', name: 'xml-path', version: '5.3.0'
  implementation group: 'org.elasticsearch', name: 'elasticsearch', version: '7.17.9'

  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.2.10.RELEASE'
  // For the below: https://stackoverflow.com/questions/64994034/bootstrap-yml-configuration-not-processed-anymore-with-spring-cloud-2020-0
  implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-bootstrap', version: '3.1.6'

  implementation group: 'org.springframework.security', name: 'spring-security-crypto', version: '5.8.3'

  implementation group: 'org.springframework', name: 'spring-jms'
  implementation group: 'org.apache.qpid', name: 'qpid-jms-client', version: '1.8.0'

  implementation group: 'com.github.hmcts', name: 'cmc-pdf-service-client', version: '7.0.1'
  implementation group: 'com.github.hmcts', name: 'send-letter-client', version: '3.0.23'
  // Remove this dependency once the secure doc-store is in use
  implementation group: 'com.github.hmcts', name: 'document-management-client', version: '7.0.1'

  implementation group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '7.0.1'

  implementation group: 'com.github.hmcts', name: 'properties-volume-spring-boot-starter', version:'0.1.1'
  implementation group: 'com.github.hmcts', name: 'java-logging', version: '6.0.1'

  implementation group: 'com.github.hmcts', name: 'service-auth-provider-java-client', version: '4.1.2'
  implementation group: 'com.github.hmcts', name: 'ccd-client', version: '4.9.2'
  implementation group: 'com.github.hmcts', name: 'idam-java-client', version: '2.1.1'

  implementation group: 'com.github.hmcts', name: 'sscs-common', version: '9dbd56a'
  implementation group: 'com.github.hmcts', name: 'sscs-pdf-email-common', version: '1.8.13'

  implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '6.2.1'

  implementation group: 'org.pitest', name: 'pitest', version: '1.13.0'
  implementation group:'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: '1.9.11'
  implementation group:'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: '0.5'

  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
  implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.28'

  implementation group: 'jakarta.jms', name: 'jakarta.jms-api', version: '3.1.0'
  implementation group: 'javax.jms', name: 'javax.jms-api', version: '2.0.1'

  implementation group: 'org.json', name: 'json', version: '20240303'
  implementation group: 'joda-time', name: 'joda-time', version: '2.12.5'

  implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'

  implementation group: 'org.apache.xmlgraphics', name: 'batik-all', version: '1.16', ext: 'pom'

  implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'

  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.2'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.2'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.2'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.2'
  testImplementation group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.9.2'
  testImplementation group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.9.2'
  testImplementation group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.9.2'

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
  testImplementation group: 'com.github.hmcts', name: 'fortify-client', version: '1.3.0', classifier: 'all', {
    exclude group: 'commons-io', module: 'commons-io'
  }
  implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

  testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
  testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
  testImplementation group: 'io.github.artsok', name: 'rerunner-jupiter', version: '2.1.6'
  testImplementation group: 'com.github.hmcts', name: 'ccd-case-document-am-client', version: '1.7.3'
  testImplementation group: 'org.glassfish', name: 'jakarta.el', version: '5.0.0-M1'
  testImplementation group: 'com.h2database', name: 'h2', version: '2.1.214'

  implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.26'
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.26'

  // Contract Tests
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.3.15'
  contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: '4.1.39'
  contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.7.12'
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'
  contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.2'
  contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.2'
  contractTestRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-commons', version: '1.9.2'
  contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '3.1.6'
  contractTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-netflix-ribbon', version: '2.2.10.RELEASE'

  contractTestImplementation sourceSets.main.runtimeClasspath
  contractTestImplementation sourceSets.test.runtimeClasspath
}

dependencyManagement {
  dependencies {
    imports {
      mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.6"
    }
    // CVE-2022-41881, CVE-2022-41915
    dependencySet(group: 'io.netty', version: '4.1.94.Final') {
      entry 'netty-buffer'
      entry 'netty-common'
      entry 'netty-codec'
      entry 'netty-codec-http'
      entry 'netty-handler'
      entry 'netty-resolver'
      entry 'netty-transport'
      entry 'netty-transport-classes-epoll'
      entry 'netty-transport-classes-kqueue'
      entry 'netty-transport-native-epoll'
      entry 'netty-transport-native-kqueue'
      entry 'netty-transport-native-unix-common'
    }
    // CVE-2023-20863
    dependencySet(group: 'org.springframework', version: '5.3.27') {
      entry 'spring-aop'
      entry 'spring-aspects'
      entry 'spring-beans'
      entry 'spring-context'
      entry 'spring-context-support'
      entry 'spring-core'
      entry 'spring-expression'
      entry 'spring-jcl'
      entry 'spring-jdbc'
      entry 'spring-orm'
      entry 'spring-tx'
      entry 'spring-web'
      entry 'spring-webmvc'
      entry 'spring-jms'
      entry 'spring-messaging'
    }
  }
}

// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

run {
  def debug = System.getProperty("debug")
  if (debug == 'true') {
    jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5006']
  }
}

mainClassName = 'uk.gov.hmcts.reform.sscs.SscsEvidenceShareApplication'

bootJar {
  getArchiveFileName().set(provider {
    'sscs-evidence-share.jar'
  })

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}
